{== START OF HEADER ==========================================================================

Program:  FirstStrike
Date:     August 2013
Platform: TradeStation v9.1

DESCRIPTION:

This is a modified version of FirstStrike trading system. It is a simple breakout system that
uses a fixed breakout range to open new positions. The original system was designed to trade
the GBP/USD.

This modification utilizes a simple moving average as a regime filter which is applied to the
daily chart. Trades are only taken in the direction of the primary trend.

More Info: http://www.systemtradersuccess.com

== END OF HEADER =============================================================================

++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Copyright � 2013. Capital Evolution, LLC. All Rights Reserved.
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

== DEFINE ALL INPUTS AND VARIABLES ==========================================================}

Input: {-------------------------------------------------------------------------------------}
Regime_Lookback(220),
BreakOutSize(50),
StopSize(60),
OpenDay(Monday),
CloseDay(Friday),
OpenTime(00),
CloseTime(1355);

Variables: {---------------------------------------------------------------------------------}
OneTickValue$(iff(GetExchangeName="FOREX", MinMove/PriceScale*BigPointValue*100000, MinMove/PriceScale*BigPointValue)),
OneTickValue(iff(GetExchangeName="FOREX", MinMove/PriceScale*100000, MinMove/PriceScale)),
BearMarket(false),
BullMarket(false),
TradeFlag(false),
cntrcts(1),
Breakout(0),
UpperBand(0),
LowerBand(0),
StopLoss$(0),
WeekDay(0);

{== START OF MAIN PROGRAM =====================================================================}

Once Begin

   Clearprintlog;
   Breakout = BreakOutSize * OneTickValue;
   StopLoss$ = cntrcts * StopSize * OneTickValue$;
   
end;

WeekDay = Dayofweek(date);

If ( Date <> Date[1] ) Then
Begin
   
   If ( Regime_Lookback > 0 ) Then
   Begin
      BearMarket = Close < Average( Close data2, Regime_Lookback );
      BullMarket = Close > Average( Close data2, Regime_Lookback );
   End
   Else Begin
      BearMarket = true;
      BullMarket = true; 
   End;
   
End;

If ( Time = OpenTime ) And ( WeekDay = OpenDay ) Then
Begin

   UpperBand = Open + Breakout;
   LowerBand = Open - Breakout;
   TradeFlag = true;
   
End;

If ( TradeFlag ) And ( MP = 0 ) Then
Begin

   If ( BullMarket ) Then Buy("LE") next bar at UpperBand stop;
   If ( BearMarket ) Then SellShort("SE") next bar at LowerBand stop;

End

Else If ( MP <> 0 ) Then 
Begin

   If ( WeekDay = Sunday ) Then
   Begin
   
      Sell("Close") next bar at market;
      Buytocover(" Close") next bar at market;
      
   End
   Else If ( WeekDay = CloseDay ) And ( Time >= CloseTime ) Then
   Begin

      Sell("Exit") next bar at market;
      Buytocover(" Exit") next bar at market;

   End;

   TradeFlag = false;

End;

Setstoploss( StopLoss$ );

{== END OF MAIN PROGRAM =====================================================================
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
Copyright � 2013. Capital Evolution, LLC. All Rights Reserved.
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++}


Jeff then took his backtesting a step further and added a trend filter.
This would limit the system to only making trades in the same direction as the long term trend,
which he defined using the 220 unit SMA. This reduced the total number of trades to
344 and increased the number of winners to 36%. It also raised the profit factor
to 1.27 and the annual return by 0.18%. While the trend filter didn’t dramatically
affect the results, it clearly was able to remove some of the unproductive trades.
